import "../../../../core"
import "../../../../services"
import "../../../../widgets"
import "."
import QtQuick
import QtQuick.Layouts
import QtQuick.Controls
import Qt5Compat.GraphicalEffects
import Quickshell
import Quickshell.Io

ColumnLayout {
    Layout.fillWidth: true
    spacing: 0
    // ── Lockscreen Section ──
    ColumnLayout {
                id: lockscreenStyleSection
                Layout.fillWidth: true
                Layout.topMargin: 12
                spacing: 4
                
                SearchHandler { 
                    searchString: "Lockscreen"
                    aliases: ["Lock", "Lock Screen"]
                }
    
                // Section Header
                RowLayout {
                    spacing: 12
                    Layout.bottomMargin: 8
    
                    MaterialSymbol {
                        text: "lock"
                        iconSize: 24
                        color: Appearance.colors.colPrimary
                    }
                    StyledText {
                        text: "Lockscreen"
                        font.pixelSize: Appearance.font.pixelSize.large
                        font.weight: Font.Medium
                        color: Appearance.colors.colOnLayer1
                        Layout.fillWidth: true
                    }
                }
    
                ColumnLayout {
                    Layout.fillWidth: true
                    spacing: 4
    
                    SegmentedWrapper {
                        Layout.fillWidth: true
                        implicitHeight: showCavaRow.implicitHeight + 32
                        orientation: Qt.Vertical
                        maxRadius: 20
                        color: Appearance.m3colors.m3surfaceContainerHigh
                        RowLayout {
                            id: showCavaRow
                            anchors.fill: parent; anchors.margins: 16
                            spacing: 16
                            MaterialSymbol { text: "equalizer"; iconSize: 24; color: Appearance.colors.colPrimary }
                            StyledText { text: "Show Cava Visualizer"; Layout.fillWidth: true; color: Appearance.colors.colOnLayer1 }
                            AndroidToggle {
                                checked: Config.ready && Config.options.lock.showCava
                                onToggled: if(Config.ready) Config.options.lock.showCava = !Config.options.lock.showCava
                            }
                        }
                    }
    
                    SegmentedWrapper {
                        Layout.fillWidth: true
                        implicitHeight: showMediaRow.implicitHeight + 32
                        orientation: Qt.Vertical
                        maxRadius: 20
                        color: Appearance.m3colors.m3surfaceContainerHigh
                        RowLayout {
                            id: showMediaRow
                            anchors.fill: parent; anchors.margins: 16
                            spacing: 16
                            MaterialSymbol { text: "movie"; iconSize: 24; color: Appearance.colors.colPrimary }
                            StyledText { text: "Show Media Controls"; Layout.fillWidth: true; color: Appearance.colors.colOnLayer1 }
                            AndroidToggle {
                                checked: Config.ready && Config.options.lock.showMediaCard
                                onToggled: if(Config.ready) Config.options.lock.showMediaCard = !Config.options.lock.showMediaCard
                            }
                        }
                    }

                    // ── Weather text color mode ────────────
                    SegmentedWrapper {
                        Layout.fillWidth: true
                        implicitHeight: weatherTextRow.implicitHeight + 36
                        orientation: Qt.Vertical
                        maxRadius: 20
                        color: Appearance.m3colors.m3surfaceContainerHigh
                        RowLayout {
                            id: weatherTextRow
                            anchors.fill: parent
                            anchors.margins: 16
                            spacing: 16
                            MaterialSymbol { text: "palette"; iconSize: 24; color: Appearance.colors.colPrimary }
                            StyledText { text: "Weather text color"; Layout.fillWidth: true; color: Appearance.colors.colOnLayer1 }
                            RowLayout {
                                spacing: 2
                                Repeater {
                                    model: [
                                        { id: "adaptive", label: "Adaptive" },
                                        { id: "light",    label: "Light" },
                                        { id: "dark",     label: "Dark" }
                                    ]
                                    delegate: SegmentedButton {
                                        required property var modelData
                                        buttonText: modelData.label
                                        isHighlighted: Config.ready && Config.options.lock && Config.options.lock.weather
                                            ? Config.options.lock.weather.textColorMode === modelData.id
                                            : modelData.id === "adaptive"
                                        colActive: Appearance.m3colors.m3primary
                                        colActiveText: Appearance.m3colors.m3onPrimary
                                        colInactive: Appearance.m3colors.m3surfaceContainerLow
                                        onClicked: if (Config.ready && Config.options.lock && Config.options.lock.weather)
                                            Config.options.lock.weather.textColorMode = modelData.id
                                    }
                                }
                            }
                        }
                    }
                }
            }
    

}
